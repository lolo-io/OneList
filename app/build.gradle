apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

android {

    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.lolo.io.onelist"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 12
        versionName "1.3"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'
            resValue "string", "app_name", "1ListDev"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            resValue "string", "app_name", "1List"
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions "store"
    productFlavors {
        fDroid {
            dimension "store"
        }
        googlePlay {
            dimension "store"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    google()
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-rc01'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.2.0-alpha01'
    implementation 'androidx.preference:preference-ktx:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'com.google.android:flexbox:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.50"
    implementation 'org.immutables:gson:2.5.6'
    implementation 'com.github.codekidX:storage-chooser:2.0.4.4'
    implementation 'com.github.skydoves:powermenu:2.1.2'
    implementation 'com.karumi:dexter:6.0.0'
    implementation 'io.github.tonnyl:whatsnew:0.1.2'
    googlePlayImplementation 'com.google.firebase:firebase-core:17.2.1'
    googlePlayImplementation 'com.google.firebase:firebase-core:17.2.1'
}

def props = new Properties()
if (rootProject.file("release.properties").exists()) {
    props.load(new FileInputStream(rootProject.file("release.properties")))
    android.signingConfigs.release.storeFile file(props.keyStore)
    android.signingConfigs.release.storePassword props.keyStorePassword
    android.signingConfigs.release.keyAlias props.keyAlias
    android.signingConfigs.release.keyPassword props.keyAliasPassword
} else {
    project.logger.info('INFO: Set the values storeFile, storePassword, keyAlias, and keyPassword in release.properties to sign the release.')
    android.buildTypes.release.signingConfig = null
}